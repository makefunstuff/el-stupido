# el-stupido GBNF grammar for llama.cpp
# Covers: one-liner fns, block fns, for loops, print, if/else, basic expressions

root        ::= ( funcdef | statement | ws )*

funcdef     ::= oneliner | blockfn
oneliner    ::= ident "(" params ")" ws "=" ws expr "\n"
blockfn     ::= "fn " ident "(" params ")" ws block "\n"

params      ::= "" | param ( "," ws param )*
param       ::= ident | ident ":" ws type

type        ::= "i32" | "i64" | "*u8"

statement   ::= declstmt | forstmt | ifstmt | printstmt | retstmt | assignstmt | exprstmt
declstmt    ::= ident ws ":=" ws expr "\n"
assignstmt  ::= ident ws ( "=" | "+=" | "-=" | "*=" ) ws expr "\n"
forstmt     ::= "for " ident " := " expr ".." expr ws block "\n"
forstmt     ::= "for " ident " := " expr "..=" expr ws block "\n"
ifstmt      ::= "if " expr ws block "\n"
ifstmt      ::= "if " expr ws block ws "el" ws block "\n"
printstmt   ::= "print(" expr ")\n"
retstmt     ::= "return " expr "\n"
exprstmt    ::= expr "\n"

block       ::= "{ " stmtlist "}"
stmtlist    ::= "" | statement stmtlist

expr        ::= ternary
ternary     ::= cmpexpr | cmpexpr ws "?" ws expr ws ":" ws expr
cmpexpr     ::= addexpr | addexpr ws cmpop ws addexpr
addexpr     ::= mulexpr | mulexpr ws [+-] ws mulexpr
mulexpr     ::= unary | unary ws [*/%] ws unary
unary       ::= callexpr | "-" callexpr | "!" callexpr
callexpr    ::= atom | atom "(" args ")"
args        ::= "" | expr | expr "," ws args

atom        ::= number | ident | "(" expr ")" | range
range       ::= number ".." number | number "..=" number | number ".." ident | number "..=" ident

number      ::= [0-9] [0-9]*
ident       ::= [a-zA-Z_] [a-zA-Z0-9_]*
cmpop       ::= "==" | "!=" | "<=" | ">=" | "<" | ">"

ws          ::= " " | "\n" | "\t" | " " ws
